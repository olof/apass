#!/usr/bin/perl
# Copyright 2011 -- 2012, Olof Johansson <olof@ethup.se>
#
# Copying and distribution of this file, with or without 
# modification, are permitted in any medium without royalty 
# provided the copyright notice are preserved. This file is 
# offered as-is, without any warranty.

our $APP = 'apass';
our $VERSION = 0.2;

use warnings FATAL => 'all';
use strict;
use feature qw/say/;

use Getopt::Long qw/:config require_order bundling/;
use App::apass::Utils::Help qw/help error version/;
use Module::Load;

our $opts = {
	file     => "$ENV{HOME}/.apass",
	length   => 16,
	cipher   => 'Rijndael',
	debug    => 0,
	help     => sub { help(\*DATA) },
	version  => sub { version() },,
};

GetOptions($opts,
	'debug',
	'file=s',
	'cipher=s',
	'help',
	'version',
);

my $cmd = shift or help(\*DATA);
exit run($cmd, $opts, @ARGV);

sub run {
	my $cmd = shift;
	my $opts = shift;

	load "App::apass::$cmd", 'invoke';
	return invoke($opts, @_);
}

__DATA__
=head1 NAME 

apass - a password generator and manager

=head1 SYNOPSIS

 apass [options] <cmd> [cmd arguments]

 See apass <cmd> --help for <cmd> specific options.

 Options:
  --file            use a database file other than ~/.apass

  --debug           print debug output to stdout
  --help            show usage information
  --version         show version information

 Commands:
  get <tag>          get password for tag
  add <tag>          generate password for new tag
  regen <tag>        generate a new password for tag
  set <tag>          manually set password for tag (new or existing)
  rename <old> <new> rename tag
  rm <tag>           remove tag
  init               initialize database

=head1 DESCRIPTION

apass is a password manager and generator, storing your passwords
in an encrypted database. When you want to access a specific site or
service, you just run apass, specifying which tag you want to 
access, and enter your master password.

=head1 COMMANDS

For detailed information on a specific command, do apass <cmd> --help.

=over

=item B<get> <tag>

Get a password associated with a tag. Takes one argument, the tag you
want to get password of.

=item B<add> <tag>

Adds a tag/password pair to the database. Will generate a random
password for you.

=item B<set> <tag>

Manually set a password for a tag (either new or already existing).

=item B<regen> <tag>

Generate a new random password for tag.

=item B<rename> <old tag> <new tag>

Rename a tag, keeping the same password information.

=item B<rm> <tag>

Remove a tag from the database.

=item B<init>

Initialize database.

=back

=head1 OPTIONS

=over

=item B<--file> B<-f>

Use an alternative location for the password database file. Default is
~/.apass.

=item B<--help> B<-h>

Print short help text (the SYNOPSIS section of this document) to stdout.

=back
